const int buttonPin = 2;   
const int redPin = 9;
const int greenPin = 10;
const int bluePin = 11;


bool buttonState = HIGH;
bool lastButtonState = HIGH;
unsigned long pressStart = 0;
bool longPress = false;

int mode = 0;                 
int colorIndex = 0;           
unsigned long lastUpdate = 0; 
bool ledOn = false;
int brightness = 0;
int fadeAmount = 5;          

int colors[6][3] = {
  {255, 0, 0},   
  {0, 255, 0},    
  {0, 0, 255},    
  {255, 255, 0},   
  {0, 255, 255}, 
  {255, 0, 255}    
};

void setup() {
  pinMode(buttonPin, INPUT_PULLUP); 
  pinMode(redPin, OUTPUT);
  pinMode(greenPin, OUTPUT);
  pinMode(bluePin, OUTPUT);
}

void setColor(int r, int g, int b) {
  analogWrite(redPin, 255 - r);
  analogWrite(greenPin, 255 - g);
  analogWrite(bluePin, 255 - b);
}

void loop() {

  buttonState = digitalRead(buttonPin);


  if (buttonState == LOW && lastButtonState == HIGH) {
    pressStart = millis();
    longPress = false;
  }


  if (buttonState == LOW && (millis() - pressStart > 1000) && !longPress) {
    longPress = true;
    mode = (mode + 1) % 3; 
  }


  if (buttonState == HIGH && lastButtonState == LOW && !longPress) {
    colorIndex = (colorIndex + 1) % 6; 
  }

  lastButtonState = buttonState;

  switch (mode) {

    case 0:
      setColor(colors[colorIndex][0], colors[colorIndex][1], colors[colorIndex][2]);
      break;

    case 1:
      if (millis() - lastUpdate > 500) { 
        lastUpdate = millis();
        ledOn = !ledOn;
      }
      if (ledOn)
        setColor(colors[colorIndex][0], colors[colorIndex][1], colors[colorIndex][2]);
      else
        setColor(0, 0, 0);
      break;

    case 2: 
      if (millis() - lastUpdate > 30) {
        lastUpdate = millis();
        brightness += fadeAmount;
        if (brightness <= 0 || brightness >= 255) fadeAmount = -fadeAmount;
      }

    
      int r = (colors[colorIndex][0] * brightness) / 255;
      int g = (colors[colorIndex][1] * brightness) / 255;
      int b = (colors[colorIndex][2] * brightness) / 255;

      analogWrite(redPin, 255 - r);
      analogWrite(greenPin, 255 - g);
      analogWrite(bluePin, 255 - b);
      break;
  }
}
